plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.0'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'nu.studer.jooq' version '8.2'
    id 'com.diffplug.spotless' version '7.0.4'
    id 'jacoco'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

jacoco {
    toolVersion = "0.8.10"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    all {
        exclude group: 'commons-logging', module: 'commons-logging'
    }
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.6'
    implementation 'org.springframework.boot:spring-boot-starter-jooq'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'com.h2database:h2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    implementation 'org.modelmapper:modelmapper:3.2.1'
    implementation 'com.diffplug.spotless:spotless-plugin-gradle:7.0.4'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

    // mybatis
    implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.4'
//    implementation 'com.github.pagehelper:pagehelper-spring-boot-starter:1.4.7'

    // jwt
    implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'

    jooqGenerator 'org.mariadb.jdbc:mariadb-java-client'

    // aws
    implementation 'io.awspring.cloud:spring-cloud-aws-starter:3.1.1'
    implementation 'com.amazonaws:aws-java-sdk-s3:1.12.767'
    implementation 'software.amazon.awssdk:s3:2.21.5'

    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

    // 메트릭 수집
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-registry-prometheus'

    // 로그 수집
    implementation 'net.logstash.logback:logstash-logback-encoder:7.4'
}

sourceSets {
    main {
        java {
            srcDir 'target/generated-sources/jooq'
        }
    }
}

spotless {
    java {
        googleJavaFormat()
        importOrder('java', 'javax', 'jakarta', 'org', 'com')
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
        target '**/*.java'
    }
}

// 테스트 실패해도 리포트 생성을 위해 설정
tasks.named('test') {
    useJUnitPlatform()
    ignoreFailures = true
    finalizedBy 'jacocoTestReport'
}

jacocoTestReport {
    dependsOn test

    reports {
        html.required.set(true)
        xml.required.set(true)
        csv.required.set(false)
        html.outputLocation = layout.buildDirectory.dir("jacocoHtml")
    }

    afterEvaluate {
        classDirectories.setFrom(
                files(classDirectories.files.collect {
                    fileTree(dir: it, include: [
                            // 원하는 패키지만 포함
                            'com/nexus/sion/feature/auth/command/application/service/**',
                            'com/nexus/sion/feature/member/command/application/service/**',
                            'com/nexus/sion/feature/member/query/service/**',
                            'com/nexus/sion/feature/notification/command/application/service/**',
                            'com/nexus/sion/feature/notification/query/service/**',
                            'com/nexus/sion/feature/project/command/application/service/**',
                            'com/nexus/sion/feature/project/query/service/**',
                            'com/nexus/sion/feature/squad/command/application/service/**',
                            'com/nexus/sion/feature/squad/query/service/**',
                            'com/nexus/sion/feature/statistics/query/service/**',
                            'com/nexus/sion/feature/techstack/command/application/service/**',
                            'com/nexus/sion/feature/techstack/query/service/**',
                    ])
                })
        )
    }
}

// JOOQ 설정
def env = new Properties()
def envFile = file(".env")
if (envFile.exists()) {
    envFile.withInputStream { stream -> env.load(stream) }
} else {
    println ".env 파일이 존재하지 않아 기본값을 사용합니다."
}
def dbHost = env.getProperty("DB_HOST", "localhost")
def dbPort = env.getProperty("DB_PORT", "3306")
def dbName = env.getProperty("DB_NAME", "sion")

def dbUrl = "jdbc:mariadb://${dbHost}:${dbPort}/${dbName}"

jooq {
    version = '3.18.6'
    configurations {
        main {
            generationTool {
                jdbc {
                    driver = 'org.mariadb.jdbc.Driver'
                    url = dbUrl
                    user = env.getProperty('DB_USERNAME')
                    password = env.getProperty('DB_PASSWORD')
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    strategy.name = 'org.jooq.codegen.DefaultGeneratorStrategy'
                    database {
                        name = 'org.jooq.meta.mariadb.MariaDBDatabase'
                        inputSchema = dbName
                    }
                    generate {
                        daos = true
                        pojos = true
                        javaTimeTypes = true
                    }
                    target {
                        packageName = 'com.example.jooq.generated'
                        directory = 'target/generated-sources/jooq'
                    }
                }
            }
        }
    }
}

tasks.named('generateJooq') {
    onlyIf {
        gradle.startParameter.taskNames.contains('generateJooq')
    }
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs << "-parameters"
}
